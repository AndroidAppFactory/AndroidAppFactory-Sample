if (rootProject.ext.pubModuleIsApplication as boolean) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// 不同类型的包，仅仅修改下面两项即可，其余内容保持一致
project.ext.applicationID = "com.bihe0832.m3u8"
project.ext.applicationName = "M3U8下载器"
project.ext.applicationPrefix = "ZM3U8"
project.ext.routerSchema = "zm3u8"

android {

    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        if (rootProject.ext.pubModuleIsApplication as boolean) {
            applicationId applicationID
        }
        multiDexEnabled true
        minSdkVersion project.appMinSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionName project.versionName
        versionCode project.versionCode
        vectorDrawables.useSupportLibrary = true
        ndk {
            abiFilters project.ndk_abiFilters_project.split(",")
            stl "stlport_static"
        }
    }

    packagingOptions {
        project.ndk_abiFilters_project.split(",").each { abiItem ->
            def finalDoNotStripFileName = "*/" + abiItem + "/*.so"
            doNotStrip finalDoNotStripFileName
        }

        project.ndk_abiFilters_all.split(",").each { abiItem ->
            project.pickFirstList.each { item ->
                def finalPickFirstFileName = "lib/" + abiItem + "/" + item
                pickFirst finalPickFirstFileName
            }
        }

        project.excludeList.each { item ->
            exclude item
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            keyAlias project.signingConfigs.keyAlias
            keyPassword project.signingConfigs.keyPassword
            storeFile file(project.signingConfigs.storeFile)
            storePassword project.signingConfigs.storePassword
            v1SigningEnabled project.signingConfigs.v1SigningEnabled as boolean
            v2SigningEnabled project.signingConfigs.v2SigningEnabled as boolean
        }

        release {
            keyAlias project.signingConfigs.keyAlias
            keyPassword project.signingConfigs.keyPassword
            storeFile file(project.signingConfigs.storeFile)
            storePassword project.signingConfigs.storePassword
            v1SigningEnabled project.signingConfigs.v1SigningEnabled as boolean
            v2SigningEnabled project.signingConfigs.v2SigningEnabled as boolean
        }
    }

    archivesBaseName = applicationPrefix + "_V${defaultConfig.versionName}_${defaultConfig.versionCode}"


    buildTypes {
        debug {
            resValue "string", "app_name", applicationName
            resValue "string", "router_schema", routerSchema
            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
        }

        release {
            resValue "string", "app_name", applicationName
            resValue "string", "router_schema", routerSchema

            signingConfig signingConfigs.release
            minifyEnabled project.minifyEnabled as boolean
            proguardFiles "./../proguard-rules-common.pro"
            shrinkResources false
        }
    }
}